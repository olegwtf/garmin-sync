#!/usr/bin/env perl

use strict;
use warnings;
use JSON::PP;
use LWP::UserAgent;
use File::Path 'mkpath';
use File::Basename;
use Term::ReadKey;

use constant APP_DIR => $ENV{HOME}.'/.config/garmin-sync';

mkpath APP_DIR or die $! unless -e APP_DIR;
my $cfg;
if (-e APP_DIR.'/garmin-sync.json') {
    $cfg = do {
        local $/;
        open my $fh, APP_DIR.'/garmin-sync.json';
        <$fh>;
    };
    
    $cfg = eval { decode_json($cfg) };
}

$cfg ||= {};

my $ua = LWP::UserAgent->new(
    agent      => 'Mozilla/5.0 (X11; Linux x86_64) Chrome/66.0.3359.117',
    timeout    => 15,
    cookie_jar => {},
    parse_head => 0,
);

unless ( $cfg->{login} && $cfg->{password} && $cfg->{path} ) {
    local $| = 1;
    print "Configure garmin-sync before first run\n";
    print "Enter your garmin connect login: ";
    chomp($cfg->{login} = <>);
    
    Term::ReadKey::ReadMode('noecho');
    print "Enter your garmin connect password (will not echo): ";
    chomp($cfg->{password} = <>);
    Term::ReadKey::ReadMode('restore');
    
    print "\nChecking credentials...\n";
    my ($ok, $error) = login($ua, $cfg->{login}, $cfg->{password});
    unless ($ok) {
        die "Provided credentials are incorrect: $error\n";
    }
    
    my %mtime;
    while (1) {
        print "Enter path to directory with garmin activities: ";
        chomp(my $directory = <>);
        unless (-d $directory) {
            die "directory should exists, did you connect your Garmin device?\n";
        }

        my $last_mtime = 0;
        my $activities = get_activities([$directory]);
        if (@{$activities->{$directory}}) {
            print "Last activity uploaded to garmin connect [".basename( $activities->{$directory}[0]{f} )."]: ";
            chomp(my $last_file = <>);
            $last_file ||= basename( $activities->{$directory}[0]{f} );

            for my $activity (@{$activities->{$directory}}) {
                if ($last_file eq basename( $activity->{f} )) {
                    $last_mtime = $activity->{m};
                    last;
                }
            }

            die "No such activity file: $last_file\n" unless $last_mtime;
        }

        $mtime{$directory} = $last_mtime;
        push @{$cfg->{path}}, $directory;

        print "Want to add one more directory? (y/n):\n";
        chomp(my $yesno = <>);
        last unless $yesno eq 'y';
    }
    
    open my $fh, '>', APP_DIR.'/last_uploaded_mtime.json' or die $!;
    print $fh encode_json(\%mtime);
    close $fh;
    
    open $fh, '>', APP_DIR.'/garmin-sync.json' or die $!;
    print $fh encode_json($cfg);
    close $fh;
    chmod 0400, APP_DIR.'/garmin-sync.json';
}

my $last_mtime = decode_json(do {
    open my $fh, APP_DIR.'/last_uploaded_mtime.json';
    <$fh> || '{}';
});

my $activities = get_activities($cfg->{path}, $last_mtime);

if (%$activities) {
    my ($ok, $error) = login($ua, $cfg->{login}, $cfg->{password});
    unless ($ok) {
        do_log("Login failed: $error");
        notify(0, "Login failed", $error);
        exit 1;
    }
    
    while (my ($dir, $activities) = each %$activities) {
        for my $activity (@$activities) {
            ($ok, $error) = upload($ua, $activity->{f});
            if ($ok) {
                if (!$last_mtime->{$dir} || $activity->{m} > $last_mtime->{$dir}) {
                    $last_mtime->{$dir} = $activity->{m};
                }

                do_log("Successfully synced " . $activity->{f});
                notify('face-smile', "Successfully synced " . basename($activity->{f}));
            }
            else {
                do_log("Can't sync " . $activity->{f} . ": $error");
                notify(0, "Can't sync " . basename($activity->{f}), $error);
            }
        }
    }
    
    open my $fh, '>', APP_DIR.'/last_uploaded_mtime.json' or die $!;
    print $fh encode_json($last_mtime);
    close $fh;
}
else {
    notify('face-confused', 'No new activities to sync');
}

sub get_activities {
    my ($dir_list, $last_mtime) = @_;
    
    my %activities;
    
    for my $dir (@$dir_list) {
        opendir my $dh, $dir or next;
        while (my $f = readdir $dh) {
            if ($f ne '.' && $f ne '..' && -f "$dir/$f") {
                $f = "$dir/$f";
                my $mtime = (stat $f)[9];

                if ($last_mtime && ($last_mtime->{$dir} || 0) >= $mtime) {
                    next;
                }

                push @{$activities{$dir}}, { 'f' => $f, 'm' => $mtime };
            }
        }

        @{$activities{$dir}} = sort { $b->{m} <=> $a->{m} } @{$activities{$dir}} if $activities{$dir};
    }
    

    
    return \%activities;
}

sub login {
    my ($ua, $login, $password) = @_;
    
    my $url = 'https://sso.garmin.com/sso/signin?service=https%3A%2F%2Fconnect.garmin.com%2Fmodern%2F&webhost=https%3A%2F%2Fconnect.garmin.com%2Fmodern%2F&source=https%3A%2F%2Fconnect.garmin.com%2Fsignin&redirectAfterAccountLoginUrl=https%3A%2F%2Fconnect.garmin.com%2Fmodern%2F&redirectAfterAccountCreationUrl=https%3A%2F%2Fconnect.garmin.com%2Fmodern%2F&gauthHost=https%3A%2F%2Fsso.garmin.com%2Fsso&locale=ru_RU&id=gauth-widget&cssUrl=https%3A%2F%2Fconnect.garmin.com%2Fgauth-custom-v1.2-min.css&privacyStatementUrl=https%3A%2F%2Fwww.garmin.com%2Fru-RU%2Fprivacy%2Fconnect%2F&clientId=GarminConnect&rememberMeShown=true&rememberMeChecked=false&createAccountShown=true&openCreateAccount=false&displayNameShown=false&consumeServiceTicket=false&initialFocus=true&embedWidget=false&generateExtraServiceTicket=true&generateTwoExtraServiceTickets=true&generateNoServiceTicket=false&globalOptInShown=true&globalOptInChecked=false&mobile=false&connectLegalTerms=true&showTermsOfUse=false&showPrivacyPolicy=false&showConnectLegalAge=false&locationPromptShown=true&showPassword=true&useCustomHeader=false&mfaRequired=false&rememberMyBrowserShown=false&rememberMyBrowserChecked=false';
    
    my $resp = $ua->get($url);
    my ($csrf) = $resp->decoded_content =~ /name="_csrf"\s+value="([^"]+)"/
        or return (0, 'no csrf token found');
    
    $resp = $ua->post(
        $url,
        [
            username => $login,
            password => $password,
            embed    => 'false',
            _csrf    => $csrf,
        ],
        Referer => $url
    );
    
    $resp->is_success
        or return (0, $resp->status_line . ' (/login)');
    
    my ($ticket) = $resp->decoded_content =~ /ticket=([^"]+)/
        or return (0, 'no id found');
    
    $resp = $ua->get("https://connect.garmin.com/modern/?ticket=$ticket");
    $resp->is_success
        or return (0, $resp->status_line . ' (/modern)');
    $resp->base !~ /signin/
        or return (0, 'something went wrong');
    
    return 1;
}

sub upload {
    my ($ua, $file) = @_;
    
    my ($ext) = $file =~ /(\.[^.]+)$/;
    my $resp = $ua->post('https://connect.garmin.com/modern/proxy/upload-service/upload/' . $ext,
        Content_Type => 'form-data',
        Content      => [file => [$file]],
        Referer      => 'https://connect.garmin.com/modern/import-data',
        NK           => 'NT',
    );
    
    my $failure = eval { decode_json($resp->decoded_content)->{detailedImportResult}{failures}[0]{messages}[0]{content} };
    unless ($resp->is_success) {
        return (0, $resp->status_line . ( $failure ? " ($failure)" : '' ));
    }
    
    if ($failure) {
        return (0, $failure);
    }
    
    return 1;
}

sub do_log {
    open my $fh, '>>', APP_DIR . '/sync.log' or die $!;
    print $fh '[', scalar(localtime), '] ', $_[0], "\n";
    close $fh;
}

sub notify {
    my ($success, $summary, $body) = @_;
    system 'notify-send', '-i', $success ? $success : 'face-angry', (!$success ? ('-t', 1000*60*60) : ()), $summary, $body||'';
}
